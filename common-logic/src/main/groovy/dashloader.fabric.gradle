import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'fabric-loom'
    id 'dashloader.definition'
}

group "net.oskarstrom.dashloader"
version "${project.definitionVersion}"

repositories {
    maven {
        name "Fabric"
        url "https://maven.fabricmc.net"
    }
    maven {
        name "TerraformersMC"
        url getTerraformersMCMaven()
    }
    mavenCentral()
    maven {
        name "Sonatype Snapshots"
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    minecraft libs.minecraft
    mappings libs.fabric.yarn
    modImplementation libs.bundles.fabric

    modImplementation libs.modmenu

    api libs.dashloader.core
    include libs.dashloader.core

    api project(":def-fabric-common")
    //TODO: do we need to JiJ it as well? if so we need to rethink the template thing
}

processResources {
    inputs.property("version", rootProject.version)

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier = null
            }
            artifact(sourcesJar) {
                classifier = "sources"
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

task createFabricModJson(type: MergeJson) {
    files = files(
            project(":def-fabric-common").file("src/main/resources/fabric.mod.json"),
            file("src/main/resources/fabric.mod.override.json")
    )
    target = file("$buildDir/generated/resources/main/fabric.mod.json")
}

processResources {
    dependsOn createFabricModJson
    exclude 'fabric.mod.override.json'
}

sourceSets {
    main {
        resources {
            srcDir "$buildDir/generated/resources/main"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier = null
            }
            artifact(sourcesJar) {
                classifier = "sources"
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
        mavenLocal()
    }
}


// Temporary hack since TerraformersMC's maven is straight up cursed
// courtesy to LambdAurora
String getTerraformersMCMaven() {
    def terraformersUrl = 'https://maven.terraformersmc.com/'
    if (pingUrl(terraformersUrl))
        return terraformersUrl
    else
        return 'https://maven.kotlindiscord.com/repository/terraformers/'
}

boolean pingUrl(String address) {
    try {
        def conn = (HttpURLConnection) new URL(address).openConnection()
        int responseCode = conn.getResponseCode()
        return (200 <= responseCode && responseCode <= 399)
    } catch (IOException ignored) {
        return false
    }
}

// blessed shit

class MergeJson extends DefaultTask {
    @InputFiles
    FileCollection files
    @OutputFile
    File target
    @TaskAction
    void concat() {
        def slurper = new JsonSlurper()
        target.withWriter { writer ->
            def json = [:]
            files.each { file ->
                json += slurper.parse(file)
            }
            writer.write(JsonOutput.prettyPrint(JsonOutput.toJson(json)))
        }
    }
}